plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}
apply from: "${project.rootDir}/gradle/dagger.gradle"

if (isDecrypted) {
    // Needs the google-services.json file to work
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    // Needs the publisher-service-account.json file to work
    apply plugin: 'com.github.triplet.play'
    play {
        serviceAccountCredentials.set(file("../../secrets/publisher-service-account.json"))
        defaultToAppBundles.set(true)
        track.set("beta")
        // Keep the strategy AUTO to continue generating "available-version-codes.txt" files
        resolutionStrategy = com.github.triplet.gradle.androidpublisher.ResolutionStrategy.AUTO
        fromTrack.set("beta")
        promoteTrack.set("production")
    }
}

android {
    defaultConfig {
        applicationId "com.masselis.tpmsadvanced"
        namespace 'com.masselis.tpmsadvanced'

        versionCode tpmsAdvancedVersionCode()
        versionName "1.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // `useTestStorageService` enables the ability to store files when capturing screenshots.
        // `clearPackageData` makes the Android Test Orchestrator run its "pm clear" command after
        // each test invocation. This command ensures that the app's state is completely cleared
        // between tests.
        testInstrumentationRunnerArguments useTestStorageService: 'true', clearPackageData: 'true'
        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }
    signingConfigs {
        if (isDecrypted)
            release {
                keyAlias APP_KEY_ALIAS
                keyPassword APP_KEY_STORE_PWD
                storeFile file(APP_KEYSTORE_LOCATION)
                storePassword APP_KEY_STORE_PWD
            }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.findByName('release') ?: signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "$composeCompilerVersion"
    }
}

dependencies {
    implementation(project(":core:common"))
    implementation(project(":core:ui"))
    implementation(project(":core:debug-ui"))
    implementation(project(":data:app"))
    implementation(project(":feature:core"))
    implementation(project(":feature:qrcode"))

    testImplementation(project(":core:test"))
    androidTestUtil 'androidx.test.services:test-services:1.4.2'
    androidTestImplementation(project(":core:android-test"))
}

tasks.whenTaskAdded { instrumentedTestTask ->
    if (instrumentedTestTask.name == "connectedDemoDebugAndroidTest") {
        task clearScreenshotPhoneFolder(type: Exec) {
            description = 'Clears the phone\'s screenshot folder'
            instrumentedTestTask.dependsOn clearScreenshotPhoneFolder
            commandLine 'adb', 'shell', 'rm -rf /sdcard/googletest/test_outputfiles'
        }
        task downloadScreenshots(type: Exec) {
            dependsOn instrumentedTestTask
            description = 'Download screenshot folder from the phone'
            doFirst {
                delete layout.buildDirectory.dir("test_outputfiles")
                mkdir layout.buildDirectory.dir("test_outputfiles")
            }
            commandLine 'adb', 'pull', '/sdcard/googletest/test_outputfiles', "$buildDir"
        }
        task copyScreenshot(type: Copy) {
            dependsOn downloadScreenshots
            group = 'publishing'
            description = 'Copy and rename the screenshots from the phone in order to be uploaded to the play store listing'
            def path = "$projectDir/src/normal/play/listings/en-US/graphics/phone-screenshots"
            doFirst {
                mkdir path
            }
            from layout.buildDirectory.dir("test_outputfiles")
            into path
            rename { filename ->
                if (filename.startsWith("light_main"))
                    return "1.png"
                else if (filename.startsWith("light_settings"))
                    return "2.png"
                else if (filename.startsWith("dark_main"))
                    return "3.png"
                else if (filename.startsWith("dark_settings"))
                    return "4.png"
                else
                    throw IllegalArgumentException(filename)
            }
        }
    }
}

// Play store listing must depends on the task which generates screenshots
if (isDecrypted) afterEvaluate {
    tasks.findAll { it.name.startsWith("publish") && it.name.endsWith("Listing") }.forEach { publishListing ->
        publishListing.dependsOn copyScreenshot
    }
}

// Removes dependency which updates the play store listing when publishing a new app in beta because
// the play store listing reflects the app in production, not the beta.
if (isDecrypted) afterEvaluate {
    tasks.findAll { it.name.startsWith("publish") && it.name.endsWith("Apps") }.forEach { task ->
        task.setDependsOn(task.dependsOn.findAll { !(it.name.startsWith("publish") && it.name.endsWith("Listing")) })
    }
}

// Create the task compareLocalVersionCodeWithPlayStore
if (isDecrypted) tasks.whenTaskAdded { processVersionCodes ->
    if (processVersionCodes.name == "processNormalReleaseVersionCodes") {
        task compareLocalVersionCodeWithPlayStore {
            dependsOn processVersionCodes
            group = 'publishing'
            description = 'Ensure the artifact to be promoted by promoteArtifact will be generated from the current commit'
            doLast {
                def playStoreVc = file("$buildDir/intermediates/gpp/normalRelease/available-version-codes.txt").text.toInteger() - 1
                assert playStoreVc == tpmsAdvancedVersionCode()
            }
        }
    }
}

// When promoting beta to production, I ensure the current commit is the one which was sent to the
// play store into by adding a dependency to the task compareLocalVersionCodeWithPlayStore
if (isDecrypted) afterEvaluate {
    tasks.findAll { it.name.startsWith("promote") && it.name.endsWith("Artifact") }.forEach { promoteArtifact ->
        promoteArtifact.dependsOn compareLocalVersionCodeWithPlayStore
    }
}
