plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

if (isDecrypted) {
    // Needs the google-services.json file to work
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'

    // Needs the publisher-service-account.json file to work
    apply plugin: 'com.github.triplet.play'
    play {
        serviceAccountCredentials.set(file("../secrets/publisher-service-account.json"))
        defaultToAppBundles.set(true)
    }
}

android {
    compileSdk 32
    defaultConfig {
        applicationId "com.masselis.tpmsadvanced"
        minSdk 27
        targetSdk 32
        versionCode commitCount()
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        if (isDecrypted)
            release {
                keyAlias APP_KEY_ALIAS
                keyPassword APP_KEY_STORE_PWD
                storeFile file(APP_KEYSTORE_LOCATION)
                storePassword APP_KEY_STORE_PWD
            }
    }
    buildTypes {
        release {
            if (signingConfigs.any { it.name == 'release' }) signingConfig signingConfigs.release
            else signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "$composeCompilerVersion"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation(project(":core"))

    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling-preview:$composeVersion"
    // Foundation (Border, Background, Box, Image, Scroll, shapes, animations, etc.)
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    // Material Design
    implementation 'androidx.compose.material3:material3:1.0.0-alpha14'
    implementation 'com.google.android.material:material:1.6.1'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.0'
    implementation "androidx.fragment:fragment:1.5.0"
    // Layout
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    // Navigation
    implementation "androidx.navigation:navigation-compose:2.5.0"

    // Tooling support (Previews, etc.)
    debugImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.1'
    debugImplementation('org.mockito.kotlin:mockito-kotlin:4.0.0') {
        exclude group: 'org.mockito', module: 'mockito-core'
    }
    debugImplementation "androidx.compose.ui:ui-tooling:$composeVersion"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$composeVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$composeVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.1'
}