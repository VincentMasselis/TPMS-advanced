// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.tpmsAdvancedVersionCode = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/main'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) + 1000
    }
}

plugins {
    id 'com.android.application' version "$agpVersion" apply false
    id 'com.android.library' version "$agpVersion" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlinVersion" apply false
    id 'org.jetbrains.kotlin.kapt' version "$kotlinVersion" apply false
    id 'com.google.gms.google-services' version '4.3.13' apply false
    id 'com.google.firebase.crashlytics' version '2.9.1' apply false
    id 'com.github.triplet.play' version '3.7.0' apply false
    id 'app.cash.sqldelight' version "$sqlDelightVersion" apply false
    id "io.gitlab.arturbosch.detekt" version "1.23.0-RC2"
    id "dev.shreyaspatil.compose-compiler-report-generator" version "1.0.0" apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

try {
    apply from: 'secrets/keys.gradle'
    ext.isDecrypted = true
    println("Project secrets decrypted")
} catch (Exception ignored) {
    ext.isDecrypted = false
    println("Project secrets encrypted")
}

apply from: 'gradle/gittag.gradle'
apply from: 'gradle/android-conf.gradle'